apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.tipz.app"
        minSdkVersion 15
        targetSdkVersion 21

        // AUTO VERSION INCREMENT SECTION
        def longVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()
        def (fullVersionTag, versionBuild, gitSha) = longVersionName.tokenize('-')
        def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')
        versionName "$versionMajor.$versionMinor.$versionPatch"
        versionCode versionMajor.toInteger() * 100000 +
                versionMinor.toInteger() * 10000 +
                versionPatch.toInteger() * 1000 +
                versionBuild.toInteger()
        printf("\n--------" + "VERSION DATA--------" + "\n" +
                "- CODE: " + versionCode + "\n" +
                "- NAME: " + versionName +
                "\n----------------------------\n")
    }

    signingConfigs {
        release {
            // Load the properties file
            Properties keysProps = new Properties()
            keysProps.load(new FileInputStream(file('../keys/tipz-production.properties')))

            // Set the store signing config values
            storeFile file('../keys/tipz-production.keystore')
            storePassword keysProps["KEY_RELEASE_STORE_PASS"]
            keyAlias 'android'
            keyPassword keysProps["KEY_RELEASE_ALIAS_PASS"]
        }

        debug {
            storeFile file('../keys/tipz-debug.keystore')
        }
    }

    buildTypes {
        release {

            signingConfig signingConfigs.release

            // Proguard section
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile file('proguard/proguard-general.txt')
            proguardFile file('proguard/proguard-retrofit.txt')
        }

        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
    }


    sourceSets {
        main {
            java {
                srcDir '../externals/wakeful/wakeful/src/'
            }
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:21.0.3"
    compile "com.android.support:cardview-v7:21.0.3"
    compile "com.android.support:recyclerview-v7:21.0.3"
    compile "com.squareup.retrofit:retrofit:1.9.0"
}